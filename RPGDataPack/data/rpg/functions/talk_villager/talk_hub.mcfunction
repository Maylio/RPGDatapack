#村人テンプレート
#summon villager ~ ~1 ~ {VillagerData:{profession:farmer,level:99,type:plains},Invulnerable:1b,CustomNameVisible:1b,Tags:["villager_can_talk","villager_quest_giver","villager_trader"],CustomName:'{"text":"田中さん","color":"yellow"}',Inventory:[{id:"minecraft:stone",Count:1b,tag:{display:{Name:'{"text":"田中さん","color":"yellow"}'}}},{id:"minecraft:stone",Count:1b,tag:{Offers:{Recipes:[{buy:{id:"minecraft:air",Count:1b,tag:{display:{Name:'{"text":"","color":"yellow"}'}}},sell:{id:"minecraft:paper",Count:1b,tag:{display:{Name:'{"text":"依頼書"}'},quest:true}},rewardExp:0b,maxUses:9999999}]}}},{id:"minecraft:stone",Count:1b,tag:{Offers:{Recipes:[{buy:{id:"minecraft:minecart",Count:1b,tag:{display:{Name:'{"text":"お財布","color":"yellow"}'}}},sell:{id:"minecraft:apple",Count:1b,tag:{display:{Name:'{"text":"リンゴ"}',Lore:['{"text":"値段：100","color":"light_purple"}']},product:true,trade_price:100,word:"おっと、足りないみたいだね",give:{id:"minecraft:apple",Count:1b}}},rewardExp:0b,maxUses:9999999},{buy:{id:"minecraft:minecart",Count:1b,tag:{display:{Name:'{"text":"お財布","color":"yellow"}'}}},sell:{id:"minecraft:apple",Count:5b,tag:{display:{Name:'{"text":"たぶん高級なリンゴ"}',Lore:['{"text":"値段：1000","color":"light_purple"}']},product:true,trade_price:1000,word:"このリンゴは特別だからね！",give:{id:"minecraft:apple",Count:5b,tag:{display:{Name:'{"text":"腐ったリンゴ"}'}},Lore:['{"text":"騙されたなポッター","color":"red"}']}}},rewardExp:0b,maxUses:9999999}]}}},{id:"minecraft:stone",Count:1b,tag:{display:{Name:'{"text":"こんにちは","color":"aqua"}'}}},{id:"minecraft:stone",Count:1b,tag:{display:{Name:'{"text":"何か買っていくかい？","color":"aqua"}'}}},{id:"minecraft:stone",Count:1b,tag:{display:{Name:'{"text":"頼みごとを聞いてくれるか？","color":"aqua"}'}}}]}

#村人に話しかけたプレイヤーが見てる村人を検知する
execute as @a[scores={talk_right=1..}] at @s positioned ~ ~1.5 ~ run function rpg:talk_villager/detect_talker

#近くにプレイヤーが居ない村人の名前をもとに戻す
execute as @e[tag=villager_can_talk,tag=villager_name_changed] at @s unless entity @a[distance=..5] run data modify entity @s CustomName set from entity @s Inventory[0].tag.display.Name
execute as @e[tag=villager_can_talk,tag=villager_name_changed] at @s unless entity @a[distance=..5] run tag @s remove villager_name_changed
#もし名前が見えて無くて、近くにプレイヤーが居た場合表示する
execute as @e[tag=villager_can_talk] unless data entity @s {CustomNameVisible:1b} at @s if entity @a[distance=..10] run data merge entity @s {CustomNameVisible:1b}
execute as @e[tag=villager_can_talk] if data entity @s {CustomNameVisible:1b} at @s unless entity @a[distance=..10] run data merge entity @s {CustomNameVisible:0b}
#スコアリセット
scoreboard players reset @a talk_right